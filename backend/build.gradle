plugins {
    id 'org.springframework.boot' version '2.0.1.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'org.luxons'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    checkstyleConfig
}

dependencies {
    compile project(':game-engine')

    compile 'org.springframework.boot:spring-boot-starter-websocket'
    compile 'org.springframework.boot:spring-boot-starter-security'
    // required by spring security with websockets
    compile 'org.springframework.security:spring-security-messaging'

    compile 'ch.qos.logback:logback-classic:1.1.8'
    compile 'org.hildan.livedoc:livedoc-springboot:4.3.2'
    compile 'org.hildan.livedoc:livedoc-ui-webjar:4.3.2'

    annotationProcessor 'org.hildan.livedoc:livedoc-javadoc-processor:4.3.2'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.hildan.jackstomp:jackstomp:1.1.0'

    checkstyleConfig "org.hildan.checkstyle:checkstyle-config:2.1.0"
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.2'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, "checkstyle.xml")
}

// packages the frontend app within the jar
jar {
    from('../frontend/build') {
        into 'static'
    }
}

// make sure we build the frontend before creating the jar
jar.dependsOn(':frontend:assemble')
