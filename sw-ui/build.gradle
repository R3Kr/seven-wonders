plugins {
    id "com.moowork.node" version "1.3.1"
}

apply plugin: 'base'

// Fix for "Could not find org.nodejs:node:10.15.3"
// Gradle node plugin + Gradle metadata seems to need this fix
// https://github.com/JetBrains/kotlin-native/issues/1612
repositories.whenObjectAdded {
    if (it instanceof IvyArtifactRepository) {
        metadataSources {
            artifact()
        }
    }
}

buildDir = 'build'

node {
    version = '10.15.3'
    yarnVersion = '1.16.0'
    download = true
}

task bundle(type: YarnTask) {
    inputs.dir(new File('public'))
    inputs.dir(new File('src'))
    outputs.dir(new File("$buildDir"))
    args = ['run', 'build']
}

task start(type: YarnTask) {
    args = ['start']
}

task test(type: YarnTask) {
    args = ['test:oneshot']
}

check.dependsOn(test)
bundle.dependsOn(yarn_install)
assemble.dependsOn(bundle)
